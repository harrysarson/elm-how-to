# How to generate runtime exceptions in elm

Elm has ["No Runtime Exceptions"](https://elm-lang.org/).
However, due to limitations in the compiler or bugs in modules that use kernal code it is still possible to generate a runtime exception if you try hard enough.
Some the the ways listed below to generate runtime exceptions will be fixed in future versions of elm.
Others probably won't be - at least when elm compiles to JavaScript.

## Stack overflow

- Ellie: <https://ellie-app.com/4tdrmxWWgM2a1>

```elm
module Main exposing (main)

import Html

recursiveDiv _ =
    Html.div [] [ recursiveDiv () ]

main =
    recursiveDiv ()
```

## Importing modules from packages with numeric GitHub owners

- Ellie <https://ellie-app.com/4tcJQwzTCzpa1>
- Issue: <https://github.com/elm/compiler/issues/1868>.
- Dependencies: [`1602/json-value`](https://package.elm-lang.org/packages/1602/json-value/latest).

```elm
module Main exposing (main)

import Html
import Json.Value

main =
    let
        json =
            Json.Value.StringValue ""
    in
       Html.text ""
```

## Browser.application with non HTTP protocol

Run in ellie (`blob:`) or by opening the html generated by `elm make` in a browser (`file:`).
Note that the issue is not directly related to this bug but if `Browser.application` cannot understand a protocol then it crashes.

- Ellie: <https://ellie-app.com/4td3dzb8X2ma1>
- Issue: <https://github.com/elm/url/issues/10>
- Offending code: <https://github.com/elm/browser/blob/1.0.1/src/Elm/Kernel/Browser.js#L185>

```elm
module Main exposing (main)

import Browser
import Browser.Navigation
import Html


update () key =
    ( ()
    , Browser.Navigation.replaceUrl key ""
    )

main : Program () () ()
main =
    Browser.application
        { init = (\() _ _ -> ((), Cmd.none))
        , onUrlChange = always ()
        , onUrlRequest = always ()
        , subscriptions = always Sub.none
        , view = always { title = "", body = [ Html.text "" ] }
        , update = (\_ _ -> ((), Cmd.none))
        }
```

## Spamming `Browser.Navigation.replaceUrl`

This only throws runtime exceptions on safari.

- Discourse: <https://discourse.elm-lang.org/t/runtime-error-due-to-browser-rate-limiting/2802>

```elm
module Main exposing (main)

import Html

main =
    Html.text "http://sscce.org/ wanted"
```

## Modulus by zero

Note that (integer) division by zero does not cause a runtime exception - [instead it gives a value of zero](https://ellie-app.com/4CB3VDzhSnHa1).

- Ellie: <https://ellie-app.com/4tdbw6YknGna1>

```elm
module Main exposing (main)

import Html

main =
    Html.text (String.fromInt (5 |> modBy 0))
```

## Comparing lists of length almost 100

- Ellie: <https://ellie-app.com/4CY9rm5SPS4a1>
- Issue: <https://github.com/elm/core/issues/1011>

```elm
module Main exposing (main)

import Html

main =
    let
        x = (List.repeat 102 True) == (List.repeat 100 True)
    in
    Html.text (Debug.toString x)
```

## Killing a process twice

- Ellie: <https://ellie-app.com/4CzHvq5XmdRa1>
- Issue: <https://github.com/elm/core/issues/955>

```elm
module Main exposing (..)

import Html
import Process
import Task exposing (Task)
import Browser


killProcessTwice : Task Never ()
killProcessTwice =
    Task.succeed ()
        |> Process.spawn
        |> Task.andThen
            (\pid ->
                Process.kill pid
                    |> Task.andThen (\_ -> Process.kill pid)
            )

init : ((), Cmd ())
init =
    ( ()
    , Task.perform (always ()) killProcessTwice
    )


main =
    Browser.element
        { init = \() -> init
        , view = \_ -> Html.text ""
        , update = \_ _ -> ((), Cmd.none)
        , subscriptions = \_ -> Sub.none
        }
```

## Comparing two functions

- Ellie: <https://ellie-app.com/4CYchNsKyxNa1>
- Explanation: <https://package.elm-lang.org/packages/elm/core/latest/Basics#==>

```elm
module Main exposing (main)

import Html

main =
    let
        x = identity == (\a -> a)
    in
    Html.text (Debug.toString x)
```
